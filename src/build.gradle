/*
 * Copyright (c) 2007-2011 Concurrent, Inc. All Rights Reserved.
 *
 * Project and contact information: http://www.cascading.org/
 *
 * This file is part of the Cascading project.
 *
 * Cascading is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Cascading is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Cascading.  If not, see <http://www.gnu.org/licenses/>.
 */

apply from: 'testing.gradle'

xmlProject = project('xml')
hadoopProject = project('hadoop')
localProject = project('local')
coreProject = project('core')
testProject = project('test')

subprojects { subproject ->
  apply plugin: 'java'
  apply plugin: 'maven'

  configure(subproject.jar) {
    manifest.attributes version: project.version
  }

  configure(subproject.artifacts) {
    archives jar
  }

  configure(install.repositories.mavenInstaller) {
    pom.artifactId = "cascading-${project.name}"
    pom.whenConfigured { pom ->
      pom.dependencies.findAll { dep ->
        dep.groupId == 'cascading'
      }.each { dep ->
        dep.artifactId = "cascading-${dep.artifactId}"
      }
    }
  }

  if( subproject.name != 'core' )
  {
    configure(subproject.dependencies) {
      compile coreProject
      runtime coreProject

      if( subproject.name == 'test' )
      {
        compile xmlProject, hadoopProject, localProject
        runtime xmlProject, hadoopProject, localProject
      }
    }
  }

  if( subproject.name != 'test' )
  {
    configure(subproject.dependencies) {
      testCompile testProject
      testRuntime testProject
    }
  }

  configure(subproject.test) {
    workingDir = rootDir
    forkEvery = 1
    jvmArgs '-Xmx756m'
  }
}
